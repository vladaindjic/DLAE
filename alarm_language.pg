AlarmQuery:
    Query
    | Query SEMICOLON Header
;

Query:
    Expr
    | Query or_op Expr
;

Expr:
    NonTerm
    | Expr and_op NonTerm
;

NonTerm:
    Term
    | not_op Term
;

Term:
    AtomicExpr
    | LPAREN Query RPAREN
;


AtomicExpr:
    RelExpr
    | TimestampExpr
    | RegExpr
;

RelExpr:
    Property rel_op RelValue
;

TimestampExpr:
    AtExpr
;

RegExpr:
    Property reg_expr_op REG_EXPR
;

AtExpr:
    Property at_op Datetime
;

Property: KEYWORD;

Header:
    CountExpr
;

CountExpr:
    COUNT LPAREN CountExprParams RPAREN
;

CountExprParams:
    CountParam
    | CountParam COMMA CountKeywordParams
;

CountParam:
    INT
;

CountKeywordParams:
    LastParam
    | GroupByParam
    | LastParam COMMA GroupByParam
    | GroupByParam COMMA LastParam
;

LastParam:
    LAST assign_op TIME_OFFSET
;

GroupByParam:
    GROUP_BY assign_op LSQUARE GroupByList RSQUARE
;

GroupByList:
    Property
    | GroupByList COMMA Property
;

RelValue:
    INT
    | DOUBLE
    | STRING
    | Datetime
;

Datetime:
    HASH DatetimeValue HASH
;

DatetimeValue:
    YEAR
    | YEAR_MONTH
    | YEAR_MONTH_DAY
    | YEAR_MONTH_DAY_HOUR
    | YEAR_MONTH_DAY_HOUR_MINUTE
    | YEAR_MONTH_DAY_HOUR_MINUTE_SECOND
    // FIXME: eventually add for ms and timezone, if needed
;


rel_op: eq_op | ne_op | lt_op | lte_op | gt_op | gte_op;
reg_expr_op: eq_op | ne_op;

// Terminal symbols
terminals

and_op: 'and' ;
or_op: 'or' ;
not_op: 'not';

lt_op: '<';
lte_op: '<=';
gt_op: '>';
gte_op: '>=';
eq_op: '==';
ne_op: '!=';

at_op: '@';
last_op: '~';

assign_op: '=';

// Lexer
KEYWORD: /\w+/;

INT: /[+-]?\d+/ ;

DOUBLE: /[+-]?((\d+\.\d*)|(\d*\.\d+))/;

// FIXME: check if this is fine regex for strings
STRING:
    /\"((\\\")|[^\"])*\"/
;

// FIXME: check if this is fine regex for regular expression in our DSL
REG_EXPR:
    /\/((\\\/)|[^\/])*\//
;


LPAREN: '(';
RPAREN: ')';
LSQUARE: '[';
RSQUARE: ']';

HASH: '#';

YEAR:
    /\d{4}/
;
YEAR_MONTH:
    /\d{4}\-\d{2}/
;
YEAR_MONTH_DAY:
    /\d{4}\-\d{2}\-\d{2}/
;
YEAR_MONTH_DAY_HOUR:
    /\d{4}\-\d{2}\-\d{2}\s+\d{2}/
;
YEAR_MONTH_DAY_HOUR_MINUTE:
    /\d{4}\-\d{2}\-\d{2}\s+\d{2}:\d{2}/
;
YEAR_MONTH_DAY_HOUR_MINUTE_SECOND:
    /\d{4}\-\d{2}\-\d{2}\s+\d{2}:\d{2}:\d{2}/
;

TIME_OFFSET:
    /(\d+(d|D)(\s+)?)?(\d+(h|H)(\s+)?)?(\d+m(\s+)?)?(\d+(s|S))?/
;

SEMICOLON: ';';
COMMA: ',';
COUNT: 'count';
LAST: 'last';
GROUP_BY: 'groupBy';